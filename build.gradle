buildscript {
    ext {
        grailsVersion = project.grailsVersion
    }
    repositories {
        mavenLocal()
        maven { url "https://repo.grails.org/grails/core" }
    }
    dependencies {
        classpath "org.grails:grails-gradle-plugin:$grailsVersion"
        classpath "org.grails.plugins:hibernate4:5.0.10"
    }
}

version "3.1.0"
group "org.grails.plugins"

apply plugin: "eclipse"
apply plugin: "idea"
apply plugin: "org.grails.grails-plugin"
//apply plugin:"org.grails.grails-plugin-publish"
apply plugin: "maven-publish"

ext {
    grailsVersion = project.grailsVersion
    gradleWrapperVersion = project.gradleWrapperVersion
    MAVEN_USERNAME = 'VDO_MAVEN_USERNAME'
    MAVEN_PASSWORD = 'VDO_MAVEN_PASSWORD'
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://repo.grails.org/grails/core" }
}

dependencyManagement {
    imports {
        mavenBom "org.grails:grails-bom:$grailsVersion"
    }
    applyMavenExclusions false
}

dependencies {
    compile "org.springframework.boot:spring-boot-starter-logging"
    compile "org.springframework.boot:spring-boot-autoconfigure"
    compile "org.grails:grails-core"
    compile "org.springframework.boot:spring-boot-starter-actuator"
    compile "org.springframework.boot:spring-boot-starter-tomcat"
    compile "org.grails:grails-dependencies"
    compile "org.grails:grails-web-boot"
    compile "org.grails.plugins:cache"
    compile "org.grails.plugins:scaffolding"
    console "org.grails:grails-console"
    profile "org.grails.profiles:web-plugin"
    provided "org.grails:grails-plugin-services"
    provided "org.grails:grails-plugin-domain-class"
    testCompile "org.grails:grails-plugin-testing"
    testCompile "org.grails.plugins:geb"
    testRuntime "org.seleniumhq.selenium:selenium-htmlunit-driver:2.47.1"
    testRuntime "net.sourceforge.htmlunit:htmlunit:2.18"

    compile 'com.hazelcast:hazelcast-wm:3.8.1'
    compile 'org.springframework.session:spring-session:1.3.1.RELEASE'
    compile 'com.hazelcast:hazelcast:3.6.8'
    compile 'org.grails.plugins:hazelcast:1.1.0'
}

/*
grailsPublish {
    githubSlug = 'budjb/grails-hazelcast-session'
    license {
        name = 'Apache-2.0'
    }
    title = "Hazelcast Session"
    developers = [budjb: "Bud Byrd"]
    repo = "grails-plugins"
}
*/

publishing {
    repositories {
        maven {
            url "https://maven.pc.rackspace.com/artifactory/plugins-release-local"
            credentials {
                username System.getenv(MAVEN_USERNAME)
                password System.getenv(MAVEN_PASSWORD)
            }
        }
    }

    publications {
        plugin(MavenPublication) {
            from components.java

            pom.withXml {
                def pomNode = asNode()
                pomNode.dependencyManagement.replaceNode {}

                // simply remove dependencies without a version
                // version-less dependencies are handled with dependencyManagement
                // see https://github.com/spring-gradle-plugins/dependency-management-plugin/issues/8 for more complete solutions
                pomNode.dependencies.dependency.findAll {
                    it.version.text().isEmpty()
                }.each {
                    it.replaceNode {}
                }

                // correct dependency scope for runtime vs compile
                asNode().dependencies.'*'.findAll() {
                    it.scope.text() == 'runtime' && project.configurations.compile.allDependencies.find { dep ->
                        dep.name == it.artifactId.text()
                    }
                }.each { it.scope*.value = 'compile' }
            }

            artifact sourcesJar
            artifact javadocJar
        }
    }
}
